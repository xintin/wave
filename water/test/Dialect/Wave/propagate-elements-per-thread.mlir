// RUN: water-opt %s --water-wave-propagate-elements-per-thread --split-input-file --verify-diagnostics --allow-unregistered-dialect | FileCheck %s

normalform.module [#wave.normal_form<full_types>] {
  func.func @register_alone() attributes {wave.hyperparameters = #wave.hyperparameters<{Y = 10, Z = 1}>, wave.constraints = []} {
    %cst = arith.constant 0.0 : f32
    // expected-error @below {{couldn't identify elements per thread for result #0}}
    wave.register %cst : !wave.tensor<[@Y, @Z] of f32, <register>>
    return
  }
}

// -----

normalform.module [#wave.normal_form<full_types>] {
  func.func @register_add() attributes {wave.hyperparameters = #wave.hyperparameters<{Y = 10, Z = 1}>, wave.constraints = []} {
    %cst = arith.constant 0.0 : f32
    // expected-error @below {{couldn't identify elements per thread for result #0}}
    %reg = wave.register %cst { elements_per_thread = 4 } : !wave.tensor<[@Y, @Z] of f32, <register>>
    wave.add %reg, %reg : (!wave.tensor<[@Y, @Z] of f32, <register>>, !wave.tensor<[@Y, @Z] of f32, <register>>) -> !wave.tensor<[@Y, @Z] of f32, <register>>
    return
  }
}

// -----

// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @propagate_register_write
func.func @propagate_register_write(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []}  {
  %cst = arith.constant 0.0 : f16
  // CHECK: wave.register {{.*}} : vector<8xf16>
  %reg = wave.register %cst : !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.write {{.*}} : vector<8xf16>, !wave.tensor<[@M] of f16, <global>>
  wave.write %reg, %mem { elements_per_thread = 8 }
     : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <global>>
  return
}
}

// -----

// Register per thread is the non-unit second element of the index map,
// propagate that in absence of explicit elements_per_thread.
//
// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @propagate_register_write_index_expr
func.func @propagate_register_write_index_expr(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []}  {
  %cst = arith.constant 0.0 : f16
  // CHECK: wave.register {{.*}} : vector<4xf16>
  %reg = wave.register %cst : !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.write {{.*}} : vector<4xf16>, !wave.tensor<[@M] of f16, <global>>
  wave.write %reg, %mem index [{M : <[] -> (<NULL>, 4, <NULL>)>}]
     : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <global>>
  return
}
}

// -----

normalform.module [#wave.normal_form<full_types>] {
func.func @propagate_register_write_index_expr_conflict(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []}  {
  %cst = arith.constant 0.0 : f16
  %reg = wave.register %cst index [{M : <[] -> (<NULL>, 4, <NULL>)>}] : !wave.tensor<[@M] of f16, <register>>
  // expected-error @below {{failed to propagate elements per thread backward: mismatch between elements_per_thread attribute (8) and operand #0 (4)}}
  wave.write %reg, %mem { elements_per_thread = 8 }
     : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <global>>
  return
}
}

// -----

// Check the error message during initialization from index expressions. In MMAs, unlike many other ops,
// we have index expressions associated with operands and not only results. This means that the
// initialization process may have assigned an EPT value to an operand when initializing dataflow for
// its defining operation, making it the only scenario in which the conflict error may be seen
// during initialization and not at some later point.
normalform.module [#wave.normal_form<full_types>] {
func.func @mma_operands_from_reads(
    %mem_a: !wave.tensor<[@M, @K] of f16, <global>>,
    %mem_b: !wave.tensor<[@N, @K] of f16, <global>>,
    %mem_c: !wave.tensor<[@M, @N] of f32, <global>>,
    %out: !wave.tensor<[@M, @N] of f32, <global>>)
  attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128, N = 64, K = 32}>, wave.constraints = [#wave.hardware_constraint<threads_per_wave = 32, waves_per_block = [1, 1, 1], mma_type = #wave.mma_kind<f32_16x16x16_f16>, vector_shapes = {M = 1, N = 1, K = 16}, max_bits_per_load = 128>]} {

  %a = wave.read %mem_a { elements_per_thread = 8 } : (!wave.tensor<[@M, @K] of f16, <global>>) -> !wave.tensor<[@M, @K] of f16, <register>>
  %b = wave.read %mem_b { elements_per_thread = 8 } : (!wave.tensor<[@N, @K] of f16, <global>>) -> !wave.tensor<[@N, @K] of f16, <register>>
  %c = wave.read %mem_c { elements_per_thread = 8 } : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>
  // expected-error @below {{failed to propagate elements per thread forward during initialization: mismatch between index expression (1) and rhs #0 (8)}}
  %result = wave.mma %a, %b, %c index [
    {M : <[] -> (<NULL>, 8, <NULL>)>, K : <[] -> (<NULL>, 1, <NULL>)>},
    {K : <[] -> (<NULL>, 1, <NULL>)>, N : <[] -> (<NULL>, 1, <NULL>)>},
    {M : <[] -> (<NULL>, 8, <NULL>)>, N : <[] -> (<NULL>, 1, <NULL>)>},
    {M : <[] -> (<NULL>, 8, <NULL>)>, N : <[] -> (<NULL>, 1, <NULL>)>}
  ] {kind =  #wave.mma_kind<f32_16x16x16_f16> } : (!wave.tensor<[@M, @K] of f16, <register>>, !wave.tensor<[@N, @K] of f16, <register>>, !wave.tensor<[@M, @N] of f32, <register>>) -> !wave.tensor<[@M, @N] of f32, <register>>
  wave.write %result, %out { elements_per_thread = 8 } : !wave.tensor<[@M, @N] of f32, <register>>, !wave.tensor<[@M, @N] of f32, <global>>
  return
}
}

// -----

// Null hyperparameters: step uses a symbol so it cannot be evaluated; pass must
// not crash and should report that EPT could not be identified.
normalform.module [#wave.normal_form<full_types>] {
  func.func @null_hyperparams_symbol_step(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.constraints = []} {
    %cst = arith.constant 0.0 : f16
    // expected-error @below {{couldn't identify elements per thread for result #0}}
    %reg = wave.register %cst index [{M : <[#wave.symbol<"M">] -> (<NULL>, M, <NULL>)>}] : !wave.tensor<[@M] of f16, <register>>
    wave.write %reg, %mem index [{M : <[#wave.symbol<"M">] -> (<NULL>, M, <NULL>)>}]
      : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <global>>
    return
  }
}

// -----

// Null hyperparameters but constant step: step has no symbols so it is
// evaluated without hyperparams and EPT is inferred.
// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @null_hyperparams_constant_step
  func.func @null_hyperparams_constant_step(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.constraints = []} {
    %cst = arith.constant 0.0 : f16
    // CHECK: wave.register {{.*}} : vector<4xf16>
    %reg = wave.register %cst : !wave.tensor<[@M] of f16, <register>>
    // CHECK: wave.write {{.*}} : vector<4xf16>, !wave.tensor<[@M] of f16, <global>>
    wave.write %reg, %mem index [{M : <[] -> (<NULL>, 4, <NULL>)>}]
      : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <global>>
    return
  }
}

// -----

// Index missing dimension N for result type [M, N]; pass must report missing dimensions.
normalform.module [#wave.normal_form<full_types>] {
  func.func @index_missing_dimension(%mem: !wave.tensor<[@M, @N] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128, N = 64}>, wave.constraints = []} {
    %cst = arith.constant 0.0 : f16
    // expected-error @below {{expected index to contain entries for all result #0 dimensions}}
    %reg = wave.register %cst index [{M : <[] -> (<NULL>, 4, <NULL>)>}] : !wave.tensor<[@M, @N] of f16, <register>>
    wave.write %reg, %mem { elements_per_thread = 4 } : !wave.tensor<[@M, @N] of f16, <register>>, !wave.tensor<[@M, @N] of f16, <global>>
    return
  }
}

// -----

// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @propagate_backward_from_write
func.func @propagate_backward_from_write(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []}  {
  %cst = arith.constant 0.0 : f16
  // CHECK: wave.register {{.*}} : vector<8xf16>
  %reg = wave.register %cst : !wave.tensor<[@M] of f16, <register>>
  %cst2 = arith.constant 42.0 : f16
  // CHECK: wave.register {{.*}} : vector<8xf16>
  %reg2 = wave.register %cst2 : !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.add {{.*}} : (vector<8xf16>, vector<8xf16>) -> vector<8xf16>
  %sum = wave.add %reg, %reg2 : (!wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.mul {{.*}} : (vector<8xf16>, vector<8xf16>) -> vector<8xf16>
  %mul = wave.mul %sum, %reg2 : (!wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.exp2 {{.*}} : (vector<8xf16>) -> vector<8xf16>
  %exp = wave.exp2 %mul : (!wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>

  // CHECK: wave.write {{.*}} : vector<8xf16>, !wave.tensor<[@M] of f16, <global>>
  wave.write %exp, %mem { elements_per_thread = 8 }
     : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <global>>
  return
}
}

// -----

// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @propagate_forward_from_read
func.func @propagate_forward_from_read(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []}  {
  // CHECK: wave.read {{.*}} : (!wave.tensor<[@M] of f16, <global>>) -> vector<4xf16>
  %reg = wave.read %mem { elements_per_thread = 4 } : (!wave.tensor<[@M] of f16, <global>>) -> !wave.tensor<[@M] of f16, <register>>
  %cst2 = arith.constant 42.0 : f16
  // CHECK: wave.register {{.*}} : vector<4xf16>
  %reg2 = wave.register %cst2 : !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.add {{.*}} : (vector<4xf16>, vector<4xf16>) -> vector<4xf16>
  %sum = wave.add %reg, %reg2 : (!wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.mul {{.*}} : (vector<4xf16>, vector<4xf16>) -> vector<4xf16>
  %mul = wave.mul %sum, %reg2 : (!wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.exp2 {{.*}} : (vector<4xf16>) -> vector<4xf16>
  %exp = wave.exp2 %mul : (!wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>

  return
}
}

// -----

// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @propagate_via_identity_rhs
func.func @propagate_via_identity_rhs(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []}  {
  // CHECK: wave.read {{.*}} : (!wave.tensor<[@M] of f16, <global>>) -> vector<4xf16>
  %reg = wave.read %mem { elements_per_thread = 4 } : (!wave.tensor<[@M] of f16, <global>>) -> !wave.tensor<[@M] of f16, <register>>
  %cst2 = arith.constant 42.0 : f16
  // CHECK: wave.register {{.*}} : vector<4xf16>
  %reg2 = wave.register %cst2 : !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.add {{.*}} : (vector<4xf16>, vector<4xf16>) -> vector<4xf16>
  %sum = wave.add %reg2, %reg : (!wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.mul {{.*}} : (vector<4xf16>, vector<4xf16>) -> vector<4xf16>
  %mul = wave.mul %reg2, %sum : (!wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>
  // CHECK: wave.exp2 {{.*}} : (vector<4xf16>) -> vector<4xf16>
  %exp = wave.exp2 %mul : (!wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>

  return
}
}

// -----

normalform.module [#wave.normal_form<full_types>] {
func.func @missing_elements_per_thread(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []}  {
  // expected-error @below {{couldn't identify elements per thread for result #0}}
  %reg = wave.read %mem : (!wave.tensor<[@M] of f16, <global>>) -> !wave.tensor<[@M] of f16, <register>>
  return
}
}

// -----

normalform.module [#wave.normal_form<full_types>] {
func.func @read_write_conflict(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []}  {
  %reg = wave.read %mem {elements_per_thread = 4} : (!wave.tensor<[@M] of f16, <global>>) -> !wave.tensor<[@M] of f16, <register>>
  // expected-error @below {{failed to propagate elements per thread backward: mismatch between elements_per_thread attribute (8) and operand #0 (4)}}
  wave.write %reg, %mem {elements_per_thread = 8} : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <global>>
  return
}
}

// -----

normalform.module [#wave.normal_form<full_types>] {
func.func @read_write_conflict_indirect(%mem: !wave.tensor<[@M] of f16, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []}  {
  %reg = wave.read %mem {elements_per_thread = 4} : (!wave.tensor<[@M] of f16, <global>>) -> !wave.tensor<[@M] of f16, <register>>
  %val = wave.exp2 %reg : (!wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>
  // expected-error @below {{failed to propagate elements per thread backward: mismatch between elements_per_thread attribute (8) and operand #0 (4)}}
  wave.write %reg, %mem {elements_per_thread = 8} : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <global>>
  return
}
}

// -----

normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: func.func @alloc_is_harmless
func.func @alloc_is_harmless() attributes {wave.hyperparameters = #wave.hyperparameters<{BLOCK_M = 4, BLOCK_K = 28, M = 128, N=128, K= 128}>, wave.constraints = []}  {
  // CHECK: wave.allocate
  %parent = wave.allocate { distributed_shape = #wave.expr_list<[] -> (256)> }
    : !wave.tensor<[@M,@N,@K] of i8, <shared>>

  // CHECK: wave.allocate
  %buf = wave.allocate in %parent : !wave.tensor<[@M,@N,@K] of i8, <shared>>
    { distributed_shape = #wave.expr_list<[#wave.symbol<"BLOCK_M">, #wave.symbol<"BLOCK_K">] -> (BLOCK_M, BLOCK_K + 4)>, offset = 128}
    : !wave.tensor<[@M, @K] of bf16, <shared>>
  return
  }
}

// -----

normalform.module [#wave.normal_form<full_types>] {
func.func @unsupported_op() attributes {wave.hyperparameters = #wave.hyperparameters<{Y = 100, Z = 200}>, wave.constraints = []}  {
  %cst = arith.constant 42.0 : f32
  %reg = wave.register %cst : !wave.tensor<[@Y, @Z] of f32, <register>>
  // expected-error @below {{cannot propagate elements per thread information across an operation not implementing the corresponding interface}}
  "foo.bar"(%reg) : (!wave.tensor<[@Y, @Z] of f32, <register>>) -> !wave.tensor<[@Y, @Z] of f32, <register>>
  return
}
}

// -----

// CHECK: normalform.module [#wave.normal_form<full_types,memory_only_types>]
normalform.module [#wave.normal_form<full_types>] {
  func.func @test_normal_form_conditions(%mem: !wave.tensor<[@M] of f32, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []} {
    %0 = arith.constant 0.0 : f32
    %reg = wave.register %0 : !wave.tensor<[@M] of f32, <register>>
    wave.write %reg, %mem { elements_per_thread = 8 } : !wave.tensor<[@M] of f32, <register>>, !wave.tensor<[@M] of f32, <global>>
    return
  }
}

// -----

// expected-error @below {{pass expects the root operation or its ancestor to guarantee the full_types normal form}}
normalform.module [] {
  func.func @normal_form_missing() {
    return
  }
}

// -----

// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @memory_resharding_allowed
func.func @memory_resharding_allowed(%mem: !wave.tensor<[@M] of f16, <shared>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []} {
  %cst = arith.constant 0.0 : f16
  // Register gets 8 elements per thread from write operation's backward propagation.
  // CHECK: wave.register {{.*}} : vector<8xf16>
  %reg8 = wave.register %cst : !wave.tensor<[@M] of f16, <register>>

  // Write 8 elements per thread to memory.
  // CHECK: wave.write {{.*}} : vector<8xf16>, !wave.tensor<[@M] of f16, <shared>>
  wave.write %reg8, %mem {elements_per_thread = 8} : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <shared>>

  // Read 4 elements per thread from same memory - this should be allowed (memory resharding).
  // CHECK: wave.read {{.*}} : (!wave.tensor<[@M] of f16, <shared>>) -> vector<4xf16>
  %reg4 = wave.read %mem {elements_per_thread = 4} : (!wave.tensor<[@M] of f16, <shared>>) -> !wave.tensor<[@M] of f16, <register>>

  return
}
}

// -----

// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @write_backward_propagation
func.func @write_backward_propagation(%mem: !wave.tensor<[@M] of f16, <shared>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []} {
  %cst = arith.constant 0.0 : f16
  // RegisterOp doesn't have explicit elements_per_thread - should get it from backward propagation.
  // CHECK: wave.register {{.*}} : vector<4xf16>
  %reg = wave.register %cst : !wave.tensor<[@M] of f16, <register>>

  // WriteOp should propagate elements_per_thread backward to register operand.
  // CHECK: wave.write {{.*}} : vector<4xf16>, !wave.tensor<[@M] of f16, <shared>>
  wave.write %reg, %mem {elements_per_thread = 4} : !wave.tensor<[@M] of f16, <register>>, !wave.tensor<[@M] of f16, <shared>>

  return
}
}

// -----

// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @read_register_propagation
func.func @read_register_propagation(%mem: !wave.tensor<[@M] of f16, <shared>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []} {
  // ReadOp should only propagate to its register result, not validate memory.
  // CHECK: wave.read {{.*}} : (!wave.tensor<[@M] of f16, <shared>>) -> vector<6xf16>
  %reg = wave.read %mem {elements_per_thread = 6} : (!wave.tensor<[@M] of f16, <shared>>) -> !wave.tensor<[@M] of f16, <register>>

  // Downstream operation should get 6 elements per thread.
  // CHECK: wave.exp2 {{.*}} : (vector<6xf16>) -> vector<6xf16>
  %result = wave.exp2 %reg : (!wave.tensor<[@M] of f16, <register>>) -> !wave.tensor<[@M] of f16, <register>>

  return
}
}

// -----

// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @mma_compute_lhs_from_rhs
func.func @mma_compute_lhs_from_rhs(%mem1: !wave.tensor<[@N, @K] of f16, <global>>, %mem2: !wave.tensor<[@M, @N] of f32, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 16, N = 16, K = 16}>, wave.constraints = [#wave.hardware_constraint<threads_per_wave = 32, waves_per_block = [1, 1, 1], mma_type = #wave.mma_kind<f32_16x16x16_f16>, vector_shapes = {M = 1, N = 1, K = 16}, max_bits_per_load = 128>]} {
  // LHS without elements_per_thread - will be computed from RHS + MMA constraints.
  %lhs_init = arith.constant 0.0 : f16
  // CHECK: wave.register {{.*}} : vector<8xf16>
  %lhs = wave.register %lhs_init : !wave.tensor<[@M, @K] of f16, <register>>

  // RHS properly initialized through read operation.
  // CHECK: wave.read {{.*}} : (!wave.tensor<[@N, @K] of f16, <global>>) -> vector<8xf16>
  %rhs = wave.read %mem1 {elements_per_thread = 8} : (!wave.tensor<[@N, @K] of f16, <global>>) -> !wave.tensor<[@N, @K] of f16, <register>>

  // ACC properly initialized through read operation.
  // CHECK: wave.read {{.*}} : (!wave.tensor<[@M, @N] of f32, <global>>) -> vector<8xf32>
  %acc = wave.read %mem2 {elements_per_thread = 8} : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>

  // LHS elements_per_thread computed via MMA backward propagation.
  // CHECK: wave.mma {{.*}} : (vector<8xf16>, vector<8xf16>, vector<8xf32>) -> vector<8xf32>
  %result = wave.mma %lhs, %rhs, %acc {kind = #wave.mma_kind<f32_16x16x16_f16>} : (!wave.tensor<[@M, @K] of f16, <register>>, !wave.tensor<[@N, @K] of f16, <register>>, !wave.tensor<[@M, @N] of f32, <register>>) -> !wave.tensor<[@M, @N] of f32, <register>>
  return
}
}

// -----

// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @mma_compute_rhs_from_lhs
func.func @mma_compute_rhs_from_lhs(%mem1: !wave.tensor<[@M, @K] of f16, <global>>, %mem2: !wave.tensor<[@M, @N] of f32, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 16, N = 16, K = 16}>, wave.constraints = [#wave.hardware_constraint<threads_per_wave = 32, waves_per_block = [1, 1, 1], mma_type = #wave.mma_kind<f32_16x16x16_f16>, vector_shapes = {M = 1, N = 1, K = 16}, max_bits_per_load = 128>]} {
  // LHS properly initialized through read operation.
  // CHECK: wave.read {{.*}} : (!wave.tensor<[@M, @K] of f16, <global>>) -> vector<8xf16>
  %lhs = wave.read %mem1 {elements_per_thread = 8} : (!wave.tensor<[@M, @K] of f16, <global>>) -> !wave.tensor<[@M, @K] of f16, <register>>

  // RHS without elements_per_thread - will be computed from LHS + MMA constraints.
  %rhs_init = arith.constant 0.0 : f16
  // CHECK: wave.register {{.*}} : vector<8xf16>
  %rhs = wave.register %rhs_init : !wave.tensor<[@N, @K] of f16, <register>>

  // ACC properly initialized through read operation.
  // CHECK: wave.read {{.*}} : (!wave.tensor<[@M, @N] of f32, <global>>) -> vector<8xf32>
  %acc = wave.read %mem2 {elements_per_thread = 8} : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>

  // RHS elements_per_thread computed via MMA backward propagation.
  // CHECK: wave.mma {{.*}} : (vector<8xf16>, vector<8xf16>, vector<8xf32>) -> vector<8xf32>
  %result = wave.mma %lhs, %rhs, %acc {kind = #wave.mma_kind<f32_16x16x16_f16>} : (!wave.tensor<[@M, @K] of f16, <register>>, !wave.tensor<[@N, @K] of f16, <register>>, !wave.tensor<[@M, @N] of f32, <register>>) -> !wave.tensor<[@M, @N] of f32, <register>>
  return
}
}

// -----

// Test MMA can compute both LHS and RHS when both are uninitialized
// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
  // CHECK-LABEL: @mma_compute_both_lhs_rhs
  func.func @mma_compute_both_lhs_rhs(%mem1: !wave.tensor<[@M, @K] of f16, <global>>, %mem2: !wave.tensor<[@N, @K] of f16, <global>>, %mem3: !wave.tensor<[@M, @N] of f32, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 16, N = 16, K = 16}>, wave.constraints = [#wave.hardware_constraint<threads_per_wave = 32, waves_per_block = [1, 1, 1], mma_type = #wave.mma_kind<f32_16x16x16_f16>, vector_shapes = {M = 1, N = 1, K = 16}, max_bits_per_load = 128>]} {
    // Both LHS and RHS without elements_per_thread - can compute from MMA formulas.
    %lhs_init = arith.constant 0.0 : f16
    // CHECK: wave.register {{.*}} : vector<8xf16>
    %lhs = wave.register %lhs_init : !wave.tensor<[@M, @K] of f16, <register>>
    %rhs_init = arith.constant 0.0 : f16
    // CHECK: wave.register {{.*}} : vector<8xf16>
    %rhs = wave.register %rhs_init : !wave.tensor<[@N, @K] of f16, <register>>

    // ACC properly initialized through read operation.
    // CHECK: wave.read {{.*}} : (!wave.tensor<[@M, @N] of f32, <global>>) -> vector<8xf32>
    %acc = wave.read %mem3 {elements_per_thread = 8} : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>

    // With proper MMA formulas, we can now compute both LHS and RHS from constraints,
    // so this should succeed instead of failing.
    // CHECK: wave.mma {{.*}} : (vector<8xf16>, vector<8xf16>, vector<8xf32>) -> vector<8xf32>
    %result = wave.mma %lhs, %rhs, %acc {kind = #wave.mma_kind<f32_16x16x16_f16>} : (!wave.tensor<[@M, @K] of f16, <register>>, !wave.tensor<[@N, @K] of f16, <register>>, !wave.tensor<[@M, @N] of f32, <register>>) -> !wave.tensor<[@M, @N] of f32, <register>>
    return
  }
}

// -----

// Test MMA error when operand has wrong elements_per_thread
normalform.module [#wave.normal_form<full_types>] {
  func.func @mma_operand_mismatch(%mem1: !wave.tensor<[@M, @K] of f16, <global>>, %mem2: !wave.tensor<[@M, @N] of f32, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{M = 16, N = 16, K = 16}>, wave.constraints = [#wave.hardware_constraint<threads_per_wave = 32, waves_per_block = [1, 1, 1], mma_type = #wave.mma_kind<f32_16x16x16_f16>, vector_shapes = {M = 1, N = 1, K = 16}, max_bits_per_load = 128>]} {
    // LHS with wrong elements_per_thread (should be 8, not 4).
    %lhs = wave.read %mem1 {elements_per_thread = 4} : (!wave.tensor<[@M, @K] of f16, <global>>) -> !wave.tensor<[@M, @K] of f16, <register>>

    // RHS without elements_per_thread - will be computed from MMA constraints.
    %rhs_init = arith.constant 0.0 : f16
    %rhs = wave.register %rhs_init : !wave.tensor<[@N, @K] of f16, <register>>

    // ACC properly initialized.
    %acc = wave.read %mem2 {elements_per_thread = 8} : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>

    // expected-error @below {{failed to propagate elements per thread backward: mismatch between computed from MMA kind (8) and LHS operand #0 (4)}}
    %result = wave.mma %lhs, %rhs, %acc {kind = #wave.mma_kind<f32_16x16x16_f16>} : (!wave.tensor<[@M, @K] of f16, <register>>, !wave.tensor<[@N, @K] of f16, <register>>, !wave.tensor<[@M, @N] of f32, <register>>) -> !wave.tensor<[@M, @N] of f32, <register>>
    return
  }
}

// -----

normalform.module [#wave.normal_form<full_types>] {
  // CHECK-LABEL: func.func @batched_mma
  func.func @batched_mma(%mem1: !wave.tensor<[@B, @M, @K] of f16, <global>>, %mem2: !wave.tensor<[@B, @N, @K] of f16, <global>>, %mem3: !wave.tensor<[@B, @M, @N] of f32, <global>>) attributes {wave.hyperparameters = #wave.hyperparameters<{B = 2, M = 16, N = 16, K = 16}>, wave.constraints = [#wave.hardware_constraint<threads_per_wave = 32, waves_per_block = [1, 1, 1], mma_type = #wave.mma_kind<f32_16x16x16_f16>, vector_shapes = {B = 1, M = 1, N = 1, K = 16}, max_bits_per_load = 128>]} {
    %lhs_init = arith.constant 0.0 : f16
    %lhs = wave.register %lhs_init : !wave.tensor<[@B, @M, @K] of f16, <register>>
    %rhs_init = arith.constant 0.0 : f16
    %rhs = wave.register %rhs_init : !wave.tensor<[@B, @N, @K] of f16, <register>>

    %acc = wave.read %mem3 {elements_per_thread = 8} : (!wave.tensor<[@B, @M, @N] of f32, <global>>) -> !wave.tensor<[@B, @M, @N] of f32, <register>>

    // CHECK: wave.mma
    // CHECK-SAME: (vector<8xf16>, vector<8xf16>, vector<8xf32>) -> vector<8xf32>
    %result = wave.mma %lhs, %rhs, %acc {kind = #wave.mma_kind<f32_16x16x16_f16>} : (!wave.tensor<[@B, @M, @K] of f16, <register>>, !wave.tensor<[@B, @N, @K] of f16, <register>>, !wave.tensor<[@B, @M, @N] of f32, <register>>) -> !wave.tensor<[@B, @M, @N] of f32, <register>>
    return
  }
}

// -----

// Test iterate working with vectors after PropagateElementsPerThread conversion
normalform.module [#wave.normal_form<full_types>] {

  // CHECK-LABEL: @iterate_with_vectors_after_ept
  func.func @iterate_with_vectors_after_ept(%mem: !wave.tensor<[@M] of f32, <global>>)
    attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128, I = 4}>,
                wave.constraints = [#wave.hardware_constraint<threads_per_wave = 64, waves_per_block = [1, 1, 1], mma_type = #wave.mma_kind<f32_32x32x8_f16>, vector_shapes = {M = 1}>]} {

    // Read into register tensor - this will become a vector after PropagateElementsPerThread.
    // CHECK: %[[INIT:.*]] = wave.read {{.*}} : (!wave.tensor<[@M] of f32, <global>>) -> vector<8xf32>
    %init = wave.read %mem {elements_per_thread = 8} : (!wave.tensor<[@M] of f32, <global>>) -> !wave.tensor<[@M] of f32, <register>>

    // Iterate should work with vectors after transformation.
    // CHECK: wave.iterate @I iter_args(%[[INIT]]) {
    %result = wave.iterate @I iter_args(%init) {
    ^bb0(%arg: !wave.tensor<[@M] of f32, <register>>):
      // Wave operations should work within the loop body after type conversion
      // CHECK: wave.add {{.*}} : (vector<8xf32>, vector<8xf32>) -> vector<8xf32>
      %doubled = wave.add %arg, %arg : (!wave.tensor<[@M] of f32, <register>>, !wave.tensor<[@M] of f32, <register>>) -> !wave.tensor<[@M] of f32, <register>>
      // CHECK: wave.yield {{.*}} : vector<8xf32>
      wave.yield %doubled : !wave.tensor<[@M] of f32, <register>>
    } : (!wave.tensor<[@M] of f32, <register>>) -> (!wave.tensor<[@M] of f32, <register>>)

    // Write should work with the vector result after iterate
    // CHECK: wave.write {{.*}} : vector<8xf32>, !wave.tensor<[@M] of f32, <global>>
    wave.write %result, %mem {elements_per_thread = 8} : !wave.tensor<[@M] of f32, <register>>, !wave.tensor<[@M] of f32, <global>>
    return
  }
}

// -----

// Test extract_slice propagates elements_per_thread as a no-op.
normalform.module [#wave.normal_form<full_types>] {

  // CHECK-LABEL: @extract_slice_propagates_ept
  func.func @extract_slice_propagates_ept(%mem: !wave.tensor<[@M, @N] of f32, <global>>)
    attributes {wave.hyperparameters = #wave.hyperparameters<{M = 64, N = 64}>, wave.constraints = []} {

    // CHECK: %[[REG:.*]] = wave.read {{.*}} : (!wave.tensor<[@M, @N] of f32, <global>>) -> vector<16xf32>
    %reg = wave.read %mem {elements_per_thread = 16} : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>

    // extract_slice should propagate elements_per_thread through (no-op trait).
    // CHECK: %[[SLICE:.*]] = wave.extract_slice %[[REG]]
    // CHECK-SAME: : (vector<16xf32>) -> vector<16xf32>
    %slice = wave.extract_slice %reg {
      offset = #wave.expr_list<[] -> (0)>,
      size = #wave.expr_list<[] -> (1)>,
      stride = #wave.expr_list<[] -> (1)>
    } : (!wave.tensor<[@M, @N] of f32, <register>>) -> !wave.tensor<[@M, @N] of f32, <register>>

    // CHECK: wave.write %[[SLICE]], {{.*}} : vector<16xf32>, !wave.tensor<[@M, @N] of f32, <global>>
    wave.write %slice, %mem {elements_per_thread = 16} : !wave.tensor<[@M, @N] of f32, <register>>, !wave.tensor<[@M, @N] of f32, <global>>
    return
  }
}

// -----

// CHECK-LABEL: @reduction_propagation_forward
normalform.module [#wave.normal_form<full_types>] {
  func.func @reduction_propagation_forward(%mem: !wave.tensor<[@M, @N] of f32, <global>>)
    attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128, N = 64}>, wave.constraints = []} {

    // CHECK: wave.read {{.*}} -> vector<8xf32>
    %reg = wave.read %mem {elements_per_thread = 8} : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>
    %c0 = arith.constant 0.0 : f32
    // CHECK: wave.register {{.*}} : vector<8xf32>
    %init = wave.register %c0 : !wave.tensor<[@M] of f32, <register>>

    // CHECK: wave.sum {{.*}} : (vector<8xf32>, vector<8xf32>) -> vector<8xf32>
    %sum = wave.sum %reg init(%init) <warp> : (!wave.tensor<[@M, @N] of f32, <register>>, !wave.tensor<[@M] of f32, <register>>) -> !wave.tensor<[@M] of f32, <register>>
    return
  }
}

// -----

normalform.module [#wave.normal_form<full_types>] {
  // CHECK-LABEL: @reduction_propagation_backward
  func.func @reduction_propagation_backward(
      %mem: !wave.tensor<[@M, @N] of f32, <global>>,
      %result_mem: !wave.tensor<[@M] of f32, <global>>)
    attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128, N = 64}>, wave.constraints = []} {

    // CHECK: wave.read {{.*}} -> vector<4xf32>
    %reg = wave.read %mem : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>
    %c0 = arith.constant 0.0 : f32
    // CHECK: wave.register {{.*}} : vector<4xf32>
    %init = wave.register %c0 : !wave.tensor<[@M] of f32, <register>>

    // CHECK: wave.sum {{.*}} : (vector<4xf32>, vector<4xf32>) -> vector<4xf32>
    %sum = wave.sum %reg init(%init) <warp> : (!wave.tensor<[@M, @N] of f32, <register>>, !wave.tensor<[@M] of f32, <register>>) -> !wave.tensor<[@M] of f32, <register>>

    // CHECK: wave.write {{.*}} : vector<4xf32>
    wave.write %sum, %result_mem {elements_per_thread = 4} : !wave.tensor<[@M] of f32, <register>>, !wave.tensor<[@M] of f32, <global>>
    return
  }
}

// -----

normalform.module [#wave.normal_form<full_types>] {
  // CHECK-LABEL: @reduction_propagation_tx
  func.func @reduction_propagation_tx(
      %mem: !wave.tensor<[@M, @N] of f32, <global>>,
      %result_mem: !wave.tensor<[@M] of f32, <global>>)
    attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128, N = 64, BLOCK_N = 16}>,
                wave.constraints = [#wave.workgroup_constraint<dim = <"N">, tile_size = <[#wave.symbol<"BLOCK_N">] -> (BLOCK_N)>, workgroup_dim = <x>>]} {

    // CHECK: wave.read {{.*}} -> vector<8xf32>
    %reg = wave.read %mem {elements_per_thread = 8} : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>
    %c0 = arith.constant 0.0 : f32
    // Reduction along thread X: init EPT matches result EPT (1), not input EPT (8).
    // CHECK: wave.register {{.*}} : vector<1xf32>
    %init = wave.register %c0 : !wave.tensor<[@M] of f32, <register>>

    // CHECK: wave.sum {{.*}} : (vector<8xf32>, vector<1xf32>) -> vector<1xf32>
    %sum = wave.sum %reg init(%init) <warp> : (!wave.tensor<[@M, @N] of f32, <register>>, !wave.tensor<[@M] of f32, <register>>) -> !wave.tensor<[@M] of f32, <register>>

    // CHECK: wave.write {{.*}} : vector<1xf32>
    wave.write %sum, %result_mem : !wave.tensor<[@M] of f32, <register>>, !wave.tensor<[@M] of f32, <global>>
    return
  }
}

// -----

normalform.module [#wave.normal_form<full_types>] {
  func.func @reduction_propagation_tx_conflict(
      %mem: !wave.tensor<[@M, @N] of f32, <global>>,
      %result_mem: !wave.tensor<[@M] of f32, <global>>)
    attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128, N = 64, BLOCK_N = 16}>,
                wave.constraints = [#wave.workgroup_constraint<dim = <"N">, tile_size = <[#wave.symbol<"BLOCK_N">] -> (BLOCK_N)>, workgroup_dim = <x>>]} {

    %reg = wave.read %mem {elements_per_thread = 8} : (!wave.tensor<[@M, @N] of f32, <global>>) -> !wave.tensor<[@M, @N] of f32, <register>>
    %c0 = arith.constant 0.0 : f32
    %init = wave.register %c0 : !wave.tensor<[@M] of f32, <register>>

    %sum = wave.sum %reg init(%init) <warp> : (!wave.tensor<[@M, @N] of f32, <register>>, !wave.tensor<[@M] of f32, <register>>) -> !wave.tensor<[@M] of f32, <register>>
    // expected-error @below {{failed to propagate elements per thread backward: mismatch between elements_per_thread attribute (8) and operand #0 (1)}}
    wave.write %sum, %result_mem { elements_per_thread = 8} : !wave.tensor<[@M] of f32, <register>>, !wave.tensor<[@M] of f32, <global>>
    return
  }
}

// -----

// Test broadcast doesn't propagate EPT.
normalform.module [#wave.normal_form<full_types>] {
  func.func @broadcast_no_propagation(%mem: !wave.tensor<[@M] of f32, <global>>)
    attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128, N = 64}>, wave.constraints = []} {

    %reg = wave.read %mem {elements_per_thread = 8} : (!wave.tensor<[@M] of f32, <global>>) -> !wave.tensor<[@M] of f32, <register>>

    // expected-error @below {{couldn't identify elements per thread for result #0}}
    %bcast = wave.broadcast %reg : (!wave.tensor<[@M] of f32, <register>>) -> !wave.tensor<[@M, @N] of f32, <register>>

    return
  }
}

// -----

// Reshape forward propagation: single operand, num_slices=2 -> result EPT = operand EPT / 2.
// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @reshape_forward_single_operand_num_slices
func.func @reshape_forward_single_operand_num_slices(
    %mem: !wave.tensor<[@M] of f32, <global>>,
    %out_mem: !wave.tensor<[@M] of f32, <global>>)
  attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []} {

  // Read 8 elements per thread.
  // CHECK: wave.read {{.*}} : (!wave.tensor<[@M] of f32, <global>>) -> vector<8xf32>
  %reg = wave.read %mem {elements_per_thread = 8} : (!wave.tensor<[@M] of f32, <global>>) -> !wave.tensor<[@M] of f32, <register>>

  // Reshape with num_slices=2: result EPT = 8 / 2 = 4.
  // CHECK: wave.reshape {{.*}} : vector<8xf32> to vector<4xf32>
  %reshaped = wave.reshape %reg {target_vector_shape = {M = 4}, num_slices = 2}
    : !wave.tensor<[@M] of f32, <register>> to !wave.tensor<[@M] of f32, <register>>

  return
}
}

// -----

// Reshape backward propagation: write fixes result EPT, operand gets result EPT * num_slices.
// CHECK: #wave.normal_form<full_types,memory_only_types>
normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @reshape_backward_single_result_num_slices
func.func @reshape_backward_single_result_num_slices(
    %mem: !wave.tensor<[@M] of f32, <global>>,
    %out_mem: !wave.tensor<[@M] of f32, <global>>)
  attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []} {

  %c0 = arith.constant 0.0 : f32
  // Register gets EPT from backward: write 4, reshape * 2 -> operand 8.
  // CHECK: wave.register {{.*}} : vector<8xf32>
  %reg = wave.register %c0 : !wave.tensor<[@M] of f32, <register>>

  // Reshape num_slices=2: result has 4, so operand gets 4 * 2 = 8.
  // CHECK: wave.reshape {{.*}} : vector<8xf32> to vector<4xf32>
  %reshaped = wave.reshape %reg {target_vector_shape = {M = 4}, num_slices = 2}
    : !wave.tensor<[@M] of f32, <register>> to !wave.tensor<[@M] of f32, <register>>

  // CHECK: wave.write {{.*}} : vector<4xf32>, !wave.tensor<[@M] of f32, <global>>
  wave.write %reshaped, %out_mem {elements_per_thread = 4}
    : !wave.tensor<[@M] of f32, <register>>, !wave.tensor<[@M] of f32, <global>>
  return
}
}

// -----

normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @reshape_forward_multiple_operands
func.func @reshape_forward_multiple_operands(
    %mem1: !wave.tensor<[@M] of f32, <global>>,
    %mem2: !wave.tensor<[@M] of f32, <global>>,
    %out_mem: !wave.tensor<[@M] of f32, <global>>)
  attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []} {

  %reg1 = wave.read %mem1 {elements_per_thread = 4} : (!wave.tensor<[@M] of f32, <global>>) -> !wave.tensor<[@M] of f32, <register>>
  %reg2 = wave.read %mem2 {elements_per_thread = 4} : (!wave.tensor<[@M] of f32, <global>>) -> !wave.tensor<[@M] of f32, <register>>

  // CHECK: wave.reshape {{.*}} : vector<4xf32> to vector<8xf32>
  %reshaped = wave.reshape %reg1, %reg2 {target_vector_shape = {M = 8}}
    : !wave.tensor<[@M] of f32, <register>> to !wave.tensor<[@M] of f32, <register>>

  return
}
}

// -----
// Backward propagation: write fixes result EPT, reshape with two operands, each operand gets result EPT / 2

normalform.module [#wave.normal_form<full_types>] {
// CHECK-LABEL: @reshape_backward_multiple_operands
func.func @reshape_backward_multiple_operands(
    %mem1: !wave.tensor<[@M] of f32, <global>>,
    %mem2: !wave.tensor<[@M] of f32, <global>>,
    %out_mem: !wave.tensor<[@M] of f32, <global>>)
  attributes {wave.hyperparameters = #wave.hyperparameters<{M = 128}>, wave.constraints = []} {

  %c0 = arith.constant 0.0 : f32
  // Both registers should get 4 elements per thread from backward: write 8, reshape 2 operands -> 8 / 2 = 4.
  // CHECK: wave.register {{.*}} : vector<4xf32>
  %reg1 = wave.register %c0 : !wave.tensor<[@M] of f32, <register>>
  // CHECK: wave.register {{.*}} : vector<4xf32>
  %reg2 = wave.register %c0 : !wave.tensor<[@M] of f32, <register>>

  // Reshape with two operands: each operand gets result EPT / 2.
  // CHECK: wave.reshape {{.*}} : vector<4xf32> to vector<8xf32>
  %reshaped = wave.reshape %reg1, %reg2 {target_vector_shape = {M = 8}}
    : !wave.tensor<[@M] of f32, <register>> to !wave.tensor<[@M] of f32, <register>>

  // CHECK: wave.write {{.*}} : vector<8xf32>, !wave.tensor<[@M] of f32, <global>>
  wave.write %reshaped, %out_mem {elements_per_thread = 8}
    : !wave.tensor<[@M] of f32, <register>>, !wave.tensor<[@M] of f32, <global>>

  return
}
}
